
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		ГраницаОстатков = Неопределено;
	Иначе
		ГраницаОстатков = МоментВремени();
	КонецЕсли;
	Запрос.УстановитьПараметр("ГраницаОстатков", ГраницаОстатков);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.Номенклатура КАК Номенклатура,
	|	ТЧ.Номенклатура.Услуга КАК ЭтоУслуга,
	|	СУММА(ТЧ.Количество) КАК Количество,
	|	СУММА(ТЧ.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТТЧ
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК ТЧ
	|ГДЕ
	|	ТЧ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧ.Номенклатура,
	|	ТЧ.Номенклатура.Услуга
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ост.Номенклатура КАК Номенклатура,
	|	Ост.Партия КАК Партия,
	|	Ост.КоличествоОстаток КАК Количество,
	|	Ост.СуммаОстаток КАК Сумма
	|ПОМЕСТИТЬ ВТОст
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ГраницаОстатков,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ВТТЧ.Номенклатура
	|				ИЗ
	|					ВТТЧ КАК ВТТЧ)) КАК Ост
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТЧ.Номенклатура КАК Номенклатура,
	|	ВТТЧ.Количество КАК КоличествоДок,
	|	ВТТЧ.Сумма КАК СуммаДок,
	|	ВТОст.Партия КАК Партия,
	|	ЕСТЬNULL(ВТОст.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВТОст.Сумма, 0) КАК Сумма,
	|	ВТТЧ.ЭтоУслуга КАК ЭтоУслуга
	|ИЗ
	|	ВТТЧ КАК ВТТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОст КАК ВТОст
	|		ПО (ВТОст.Номенклатура = ВТТЧ.Номенклатура)
	|ГДЕ
	|	НЕ ВТТЧ.ЭтоУслуга
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТОст.Партия.МоментВремени
	|ИТОГИ
	|	МИНИМУМ(КоличествоДок),
	|	МИНИМУМ(СуммаДок),
	|	СУММА(Количество)
	|ПО
	|	Номенклатура";
	Если УчетнаяПолитика.МетодСписания <> Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	Результат = Запрос.Выполнить();
	ВыборкаНоменклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.КоличествоДок > ВыборкаНоменклатура.Количество Тогда
			Отказ = Истина;
			Сообщить(СтрШаблон("Недостаточно %1 (из %2 есть %3)",
				ВыборкаНоменклатура.Номенклатура, ВыборкаНоменклатура.КоличествоДок, ВыборкаНоменклатура.Количество));
			Продолжить;
		КонецЕсли;
		ВыборкаПартии = ВыборкаНоменклатура.Выбрать();
		Списать = ВыборкаНоменклатура.КоличествоДок;
		Пока ВыборкаПартии.Следующий() И Списать > 0 Цикл
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Партия = ВыборкаПартии.Партия;
			Движение.Количество = Мин(Списать, ВыборкаПартии.Количество);
			Списать = Списать - Движение.Количество;
			Движение.Сумма = Движение.Количество / ВыборкаПартии.Количество * ВыборкаПартии.Сумма;
		КонецЦикла;
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.Записать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(ОстаткиНоменклатуры.Количество) КАК Количество,
	|	СУММА(ОстаткиНоменклатуры.Сумма) КАК Себестоимость
	|ПОМЕСТИТЬ ВТ_Себестоимость
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры КАК ОстаткиНоменклатуры
	|ГДЕ
	|	ОстаткиНоменклатуры.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТЧ.Номенклатура КАК Номенклатура,
	|	ВТТЧ.Количество КАК Количество,
	|	ВТТЧ.Сумма КАК Сумма,
	|	ВТ_Себестоимость.Себестоимость КАК Себестоимость
	|ИЗ
	|	ВТТЧ КАК ВТТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Себестоимость КАК ВТ_Себестоимость
	|		ПО ВТТЧ.Номенклатура = ВТ_Себестоимость.Номенклатура";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
КонецПроцедуры
